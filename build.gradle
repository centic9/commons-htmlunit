buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.thetaphi:forbiddenapis:3.10'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    // https://github.com/SgtSilvio/gradle-maven-central-publishing
    id 'io.github.sgtsilvio.gradle.maven-central-publishing' version '0.4.0'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

apply plugin: 'de.thetaphi.forbiddenapis'
apply plugin: 'jacoco'
apply plugin: 'signing'

java {
    toolchain {
        sourceCompatibility = JavaVersion.VERSION_17
    }
}

group = 'org.dstadler'
base {
    archivesName = 'commons-htmlunit'
}

repositories {
    mavenCentral()
}

forbiddenApis {
    suppressAnnotations = ['org.dstadler.commons.util.SuppressForbidden']
    bundledSignatures = [ 'jdk-reflection', 'commons-io-unsafe-2.19.0', 'jdk-internal' ]
}
forbiddenApisMain {
    // 'jdk-unsafe', 'jdk-system-out'
    bundledSignatures += [ 'jdk-deprecated', 'jdk-internal', 'jdk-non-portable' ]
}

dependencies {
    api 'org.htmlunit:htmlunit:4.18.0'
    implementation 'com.google.guava:guava:33.5.0-jre'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.0'

    testImplementation 'org.dstadler:commons-test:1.0.0.22'
    testImplementation 'org.dstadler:commons-dost:1.4.3'
}

wrapper {
    gradleVersion = '8.14.3'
}

test {
    systemProperties = System.properties as Map<String, ?>

    // enable to show standard out and standard error of the test JVM(s) on the console
    // testLogging.showStandardStreams = true

    // http://forums.gradle.org/gradle/topics/jacoco_related_failure_in_multiproject_build
    systemProperties['user.dir'] = workingDir

    // avoid failures because Gradle provides it's own version of JNA in the system properties
    systemProperties['jna.boot.library.path'] = null
    useJUnitPlatform()
}

test.dependsOn jar

jacoco {
    toolVersion = '0.8.14'
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

tasks.withType(Jar).configureEach { task ->
    task.doLast {
        ant.checksum algorithm: 'md5', file: it.archivePath
        ant.checksum algorithm: 'sha1', file: it.archivePath
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'commons-htmlunit'
            from components.java

            pom {
                name = 'Common HtmlUnit Helpers'
                description = 'Common HtmlUnit utility methods that I find useful in various projects.'
                url = 'https://github.com/centic9/commons-htmlunit'
                licenses {
                    license {
                        name = 'BSD 2-Clause "Simplified" License'
                        url = 'https://github.com/centic9/commons-htmlunit/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'centic9'
                        name = 'Dominik Stadler'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:centic9/commons-htmlunit'
                    developerConnection = 'scm:git@github.com:centic9/commons-htmlunit'
                    url = 'https://github.com/centic9/commons-htmlunit'
                }
            }
        }
    }
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            //url = layout.buildDirectory.dir('repo')
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials(PasswordCredentials)
            //snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/')
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
